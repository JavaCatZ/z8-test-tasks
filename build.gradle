buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'http://mvn.revoltsoft.ru/' }
		maven { url 'https://plugins.gradle.org/m2/' }
	}
	dependencies {
		classpath 'com.eriwen:gradle-css-plugin:2.14.0'
		classpath 'com.eriwen:gradle-js-plugin:2.14.1'
	}
}

buildscript {
	dependencies {
		classpath 'gradle.plugin.com.dorongold.plugins:task-tree:1.5'
	}
}

apply plugin: 'com.dorongold.task-tree'

repositories {
	mavenLocal()
	maven { url 'http://mvn.revoltsoft.ru/' }
}

ext.buildFolder = './target'

buildDir = buildFolder

ext.z8Version = '1.3.0'
if (System.properties['z8.home'] != null)
	ext.z8Home = file(System.properties['z8.home'])

apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'war'
apply plugin: 'js'
apply plugin: 'css'
//apply plugin: 'maven'
//apply plugin: 'maven-publish'

configurations {
	compiler
	boot
	if (!project.hasProperty('z8Home')) {
		webzip { transitive = false }
		reszip { transitive = false }
		blzip { transitive = false }
	}
}

dependencies {
	implementation "org.zenframework.z8:org.zenframework.z8.server:${z8Version}"
	implementation "org.zenframework.z8:org.zenframework.z8.lang:${z8Version}"
	runtime "org.zenframework.z8:org.zenframework.z8.webserver:${z8Version}"
	runtime "org.zenframework.z8:org.zenframework.z8.oda.driver:${z8Version}"
	runtime "org.zenframework.z8.dependencies.jdbc:postgresql-42.0.0:3.0"

	boot "org.zenframework.z8:org.zenframework.z8.boot:${z8Version}"

	if (!project.hasProperty('z8Home')) {
		blzip "org.zenframework.z8:org.zenframework.z8.lang:${z8Version}:bl" //fileTree("${z8Home}/org.zenframework.z8.lang/target/libs") { include '*.zip' }
		webzip "org.zenframework.z8:org.zenframework.z8.js:${z8Version}:web" //fileTree("${z8Home}/org.zenframework.z8.js/target/libs") { include '*.zip' }
		reszip "org.zenframework.z8:org.zenframework.z8.resources:${z8Version}:res" //fileTree("${z8Home}/org.zenframework.z8.resources/target/libs") { include '*.zip' }
	}
	
	compiler "org.zenframework.z8:org.zenframework.z8.compiler:${z8Version}"
}

eclipse {
	// Z8 nature
	project.natures 'org.zenframework.z8.pde.ProjectNature'
	// Eclipse: default java output -> $buildDir/classes/default
	classpath.defaultOutputDir = new File(buildDir, 'classes/main')
	// Eclipse: java source folders output -> $buildFolder/classes/...
	classpath.file.whenMerged {
		entries.findAll { entry ->
			entry instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder
		}.each { entry ->
			entry.output = entry.output.replace('bin/', "${buildFolder}/classes/")
		}
		if (!project.hasProperty('z8Home'))
			entries += new org.gradle.plugins.ide.eclipse.model.ProjectDependency('/org.zenframework.z8.lang')
	}
}

// Prepare resources

ext.resDepsPath = project.hasProperty('z8Home') ? "${z8Home}/org.zenframework.z8.resources/src"
: "${System.properties['java.io.tmpdir'] ?: '/tmp'}/${project.name}/dependencies/resources"

task unpackReszip(type: Copy) {
	if (!project.hasProperty('z8Home')) {
		from zipTree(configurations.reszip.singleFile.absolutePath)
		into resDepsPath
	}
}

task defaultResources(type: Copy) {
	if (!project.hasProperty('z8Home'))
		dependsOn unpackReszip
	from resDepsPath
	into "${projectDir}/src/main"
	eachFile {
		if (it.relativePath.getFile(destinationDir).exists()) {
			it.exclude()
		}
	}
}

// Bl & Java config

ext.blDepsPath = project.hasProperty('z8Home') ? "${z8Home}/org.zenframework.z8.lang"
		: "${System.properties['java.io.tmpdir'] ?: '/tmp'}/${project.name}/dependencies/bl"
ext.blOutput = "${projectDir}/.java"

task unpackBlzip(type: Copy) {
	if (!project.hasProperty('z8Home')) {
		from zipTree(configurations.blzip.singleFile.absolutePath)
		into blDepsPath
	}
}

task compileBl(type: JavaExec) {
	if (!project.hasProperty('z8Home'))
		dependsOn unpackBlzip

	group = 'Build'
	description = 'Compile BL sources'

	classpath = configurations.compiler
	main = 'org.zenframework.z8.compiler.cmd.Main'
	args = [
		projectDir,
		"-projectName:${project.name}",
		"-output:${blOutput}",
		"-requires:${blDepsPath}"
	]
}

tasks.withType(JavaCompile) {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

sourceSets {
	main {
		java.srcDirs blOutput
		java.outputDir = file("${buildDir}/classes/main")
		resources.srcDirs blOutput
		resources.outputDir = file("${buildDir}/classes/main")
	}
}

// Web config

ext.webDepsPath = project.hasProperty('z8Home') ? "${z8Home}/org.zenframework.z8.js/target/web"
		: "${System.properties['java.io.tmpdir'] ?: '/tmp'}/${project.name}/dependencies/web"

// Unzip webzip dependencies
task unpackWebzip(type: Copy) {
	if (!project.hasProperty('z8Home')) {
		from zipTree(configurations.webzip.singleFile.toPath())
		into webDepsPath
	}
}

combineCss {
	if (!project.hasProperty('z8Home'))
		dependsOn unpackWebzip
	source = [ "${webDepsPath}/debug/css/revolt.css" ].plus(
			file("$projectDir/src/main/css/css.buildorder").readLines().collect { "$projectDir/src/main/css/$it" })
	dest = file("${buildDir}/web/debug/css/${project.name}.css")
}

minifyCss {
	source = combineCss
	dest = "${buildDir}/web/css/${project.name}.css"
	closure { warningLevel = 'QUIET' }
}

combineJs {
	if (project.hasProperty('z8Home'))
		dependsOn unpackWebzip
	source = [ "${webDepsPath}/debug/revolt.js" ].plus(
			file("$projectDir/src/main/js/js.buildorder").readLines().collect { "$projectDir/src/main/js/$it" })
	dest = file("${buildDir}/web/debug/${project.name}.js")
}

minifyJs {
	source = combineJs
	dest = file("${buildDir}/web/${project.name}.js")
	closure { warningLevel = 'QUIET' }
}

task prepareWeb(type: Copy) {
	dependsOn defaultResources
	from('src/main/webapp') {
		filesMatching('**/*.html') {
			expand project: project
		}
	}
	into "${buildDir}/web"
}

task prepareDebug(type: Copy) {
	dependsOn defaultResources
	from('src/main/webapp') {
		exclude 'WEB-INF/**'
		exclude 'debug.html'
		filesMatching('**/*.html') {
			expand project: project
		}
	}
	into "${buildDir}/web/debug"
}

task assembleWeb(dependsOn: [minifyCss, minifyJs, prepareWeb, prepareDebug])

// Application config

application {
	mainClassName = 'org.zenframework.z8.server.engine.ServerMain'
}

ext.appJavaXmx = '2048M'
ext.appHost = java.net.InetAddress.getLocalHost().getHostAddress()
ext.appPort = 9080
ext.appMaxFormContentSize = 15000000

run {
	dependsOn assembleWeb
	jvmArgs = [
		"-Xmx${appJavaXmx}",
		"-Xbootclasspath/p:${configurations.boot.singleFile.toPath()}",
		"-Dorg.eclipse.jetty.server.Request.maxFormContentSize=${appMaxFormContentSize}",
		"-Dorg.mortbay.http.HttpRequest.maxFormContentSize=${appMaxFormContentSize}",
		"-Djava.rmi.server.hostname=${appHost}",
		"-Dz8.webserver.port=${appPort}",
		"-Dz8.webserver.webapp=${buildDir}/web"
	]
	args = ['-server', 'webserver']
	workingDir = "${buildDir}/web/WEB-INF"
}

// WAR config

war {
	dependsOn assembleWeb
	exclude('debug.html')
	from("${buildDir}/web") {
		include '**/*.css'
		include '**/*.js'
	}
	filesMatching('**/*.html') {
		expand project: project
	}

	// TODO Exclude WEB-INF/lib/*.pom files
	//exclude('**/*.pom')
}

// Distribution config

distributions {
	main {
		contents {
			from(buildDir) {
				include 'web/**'
				//exclude 'web/debug/**'
			}
			from("$projectDir/src/main") {
				include 'bin/**'
				expand project: project
			}
		}
	}
}

distZip.dependsOn assembleWeb
distTar.dependsOn assembleWeb

//publishing {
//	repositories {
//		mavenLocal()
//	}
//}
